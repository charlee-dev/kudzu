buildscript {
    ext.kotlin_version = '1.3.21'
    ext.orchid_version = '0.16.0'
    ext.strikt_version = '0.15.2'
    ext.junit_version = '5.1.0'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    configurations.maybeCreate("pitest")
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.eden:orchidPlugin:$orchid_version"
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
        pitest 'org.pitest:pitest-junit5-plugin:0.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'com.eden.orchidPlugin'
apply plugin: "info.solidsoft.pitest"

group = "com.copper-leaf"
version = "0.1.0"

repositories {
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/javaeden/Orchid/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"

    // testing
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testCompile "io.strikt:strikt-core:$strikt_version"

    // Orchid
    orchidCompile "io.github.javaeden.orchid:OrchidCore:$orchid_version"

    orchidRuntime "io.github.javaeden.orchid:OrchidCore:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidBsDoc:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidPages:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidPluginDocs:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidSearch:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidKotlindoc:$orchid_version"
}

sourceCompatibility = JavaVersion.VERSION_1_6
compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}
test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}
pitest {
    targetClasses = ['com.copperleaf.kudzu.*']
    threads = 8
    outputFormats = ['HTML', 'XML']
    jvmArgs = ['-Xmx1024m']
    testPlugin = 'junit5'
}
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

task deploy {
    doLast {}
}

// Orchid config
//----------------------------------------------------------------------------------------------------------------------

orchid {
    version = "${project.version}"
    theme = "BsDoc"

    if (project.hasProperty('env') && project.property('env') == 'prod') {
        baseUrl = "https://copper-leaf.github.io/kudzu"
        environment = 'prod'
    } else {
        baseUrl = "http://localhost:8080"
        environment = 'debug'
    }

    args = ["githubToken ${project.hasProperty("github_token") ? project.property("github_token") : System.getenv('GITHUB_TOKEN')}"]
}
tasks.build.dependsOn orchidBuild
tasks.check.dependsOn 'pitest'
tasks.check.dependsOn jacocoTestReport
tasks.jacocoTestReport.dependsOn 'pitest'
tasks.deploy.dependsOn orchidDeploy

// Code Coverage Reports
//----------------------------------------------------------------------------------------------------------------------
task codeCoverageReport(type: JacocoReport) {
    dependsOn test

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    sourceSets sourceSets.main

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

configurations { codacy }
dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:4.0.2'
}
task sendCoverageToCodacy(type: JavaExec) {
    dependsOn codeCoverageReport

    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l", "Java",
            "-r", "${buildDir}/reports/jacoco/report.xml",
            "-t", "${System.getenv("CODACY_PROJECT_TOKEN")}",
            "--language", "Kotlin"
    ]
}
